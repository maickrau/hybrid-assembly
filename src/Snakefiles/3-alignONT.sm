#############################################################################
 #
 #  This file is part of Verkko, a software program that assembles
 #  whole-genome sequencing reads into telomere-to-telomere
 #  haplotype-resolved chromosomes.
 #
 #  Except as indicated otherwise, this is a 'United States Government
 #  Work', and is released in the public domain.
 #
 #  File 'README.licenses' in the root directory of this distribution
 #  contains full conditions and disclaimers.
 #
 ##

# 
#  Rules indexGraph and alignONT map the (partitioned) ONT reads to the HiFi
#  graph out of MBG.
#
#  There's no magic here, except that we compute the GraphAligner index
#  prefix from the snamekmake output.index (or input.index) path name on the
#  fly.
#

rule indexGraph:
    input:
        gfa               = rules.processGraph.output.gfa
    output:
        index             = "3-align/graph.index",
        diploid_index     = "3-align/diploid.index" if config['haploid'] == "False" else rules.emptyfile.output[0]
    log:
        err               = "3-align/index.err"
    params:
        GRAPHALIGNER      = config.get('GRAPHALIGNER', "{VERKKO}/bin/GraphAligner"),
        bandwidth         = config['ali_bandwidth'],
        mxm_length        = config['ali_mxm_length'],
        mem_count         = config['ali_mem_count'],
        haploid           = config['haploid']
    threads:
        1
    resources:
        job_id = 1,
        n_cpus = 1,
        mem_gb = lambda wildcards, input, attempt: getAlignMemoryRequest(attempt, 15, [ input.gfa ]),
        time_h = lambda wildcards, input, attempt: getTimeRequest(attempt, 'ali')
    shell:
        '''
cd 3-align

cat > ./index.sh <<EOF
#!/bin/sh
set -e

prefix=\\$(echo ../{output.index} | sed s/.index//)
echo "Saving index to prefix \\"\\$prefix\\""
echo ""

touch empty.fasta

if [ "{params.haploid}" = "False" ]; then
  diploid="--diploid-heuristic 21 31 --diploid-heuristic-cache ../{output.diploid_index}"
else
   touch ../{output.diploid_index}
fi

{params.GRAPHALIGNER} -t {threads} -g ../{input.gfa} -f empty.fasta -a empty.gaf \\\\
  \\$diploid \\\\
  --seeds-mxm-cache-prefix \\$prefix \\\
  --bandwidth {params.bandwidth} \\\\
  --seeds-mxm-length {params.mxm_length} \\\\
  --mem-index-no-wavelet-tree \\\\
  --seeds-mem-count {params.mem_count} && touch ../{output.index}

rm -f empty.gaf empty.fasta
EOF

chmod +x ./index.sh

./index.sh > ../{log.err} 2>&1
        '''



rule alignONT:
    input:
        ont_reads         = '3-align/split/ont{nnnn}.fasta.gz',
        index             = rules.indexGraph.output.index,
        diploid_index     = rules.indexGraph.output.diploid_index if config['haploid'] == "False" else rules.emptyfile.output[0],
        gfa               = rules.processGraph.output.gfa
    output:
        alignments        = '3-align/aligned{nnnn}.gaf'
    log:
        err               = '3-align/aligned{nnnn}.err'
    params:
        GRAPHALIGNER      = config['GRAPHALIGNER'],

        alignmentstmp     = '3-align/aligned{nnnn}.WORKING.gaf',
        mxm_length        = config['ali_mxm_length'],
        mem_count         = config['ali_mem_count'],
        bandwidth         = config['ali_bandwidth'],
        multi_score_f     = config['ali_multi_score_f'],
        clipping          = config['ali_clipping'],
        min_score         = config['ali_min_score'],
        end_clipping      = config['ali_end_clipping'],
        incompat_cutoff   = config['ali_incompat_cutoff'],
        max_trace         = config['ali_max_trace'],
        mem_window        = config['ali_seed_window'],
        haploid           = config['haploid']
    threads:
        int(config['ali_n_cpus'])
    resources:
        job_id = lambda wildcards, input, attempt: int(wildcards.nnnn),
        n_cpus = int(config['ali_n_cpus']),
        mem_gb = lambda wildcards, input, attempt: getAlignMemoryRequest(attempt, 4, [ input.index, input.diploid_index ]),
        time_h = lambda wildcards, input, attempt: getTimeRequest(attempt, 'ali', 3)
    shell:
        '''
cd 3-align

cat > ./aligned{wildcards.nnnn}.sh <<EOF
#!/bin/sh
set -e

prefix=\\$(echo ../{input.index} | sed s/.index//)
echo "Using index from prefix \\"\\$prefix\\""
echo ""

if [ {params.mem_window} -gt 0 ]; then
   memwindow="--seeds-mxm-windowsize {params.mem_window}"
fi

if [ "{params.haploid}" = "False" ]; then
  diploid="--diploid-heuristic 21 31 --diploid-heuristic-cache ../{input.diploid_index}"
fi

{params.GRAPHALIGNER} -t {threads} -g ../{input.gfa} -f ../{input.ont_reads} -a ../{params.alignmentstmp} \\\\
  \\$diploid \\\\
  --seeds-mxm-cache-prefix \\$prefix \\\\
  \\$memwindow \\\\
  --seeds-mxm-length {params.mxm_length} \\\\
  --seeds-mem-count {params.mem_count} \\\\
  --bandwidth {params.bandwidth} \\\\
  --multimap-score-fraction {params.multi_score_f} \\\\
  --precise-clipping {params.clipping} \\\\
  --min-alignment-score {params.min_score} \\\\
  --hpc-collapse-reads \\\\
  --discard-cigar \\\\
  --clip-ambiguous-ends {params.end_clipping} \\\\
  --overlap-incompatible-cutoff {params.incompat_cutoff} \\\\
  --max-trace-count {params.max_trace} \\\\
  --mem-index-no-wavelet-tree \\\\
&& \\\\
mv -f ../{params.alignmentstmp} ../{output.alignments}
EOF

chmod +x ./aligned{wildcards.nnnn}.sh

./aligned{wildcards.nnnn}.sh > ../{log.err} 2>&1
        '''
