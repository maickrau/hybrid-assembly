#############################################################################
 #
 #  This file is part of Verkko, a software program that assembles
 #  whole-genome sequencing reads into telomere-to-telomere
 #  haplotype-resolved chromosomes.
 #
 #  Except as indicated otherwise, this is a 'United States Government
 #  Work', and is released in the public domain.
 #
 #  File 'README.licenses' in the root directory of this distribution
 #  contains full conditions and disclaimers.
 #
 ##

#
#  Rule splitONT rewrites the input ONT reads into partitioned FASTA files
#  for mapping to the unitig graph in parallel.
#
#  Configuration Parameters:
#    spl_bases
#    spl_reads
#

checkpoint splitONT:
    input:
        ont      = ONT_READS if config['withONT'] == "True" else {rules.emptyfile.output}
    output:
        finished = '3-align/split/splitONT.finished'
    log:
        err      = '3-align/splitONT.err'
    params:
        bases    = config['spl_bases'],
        reads    = config['spl_reads'],
        length   = config['spl_min_length']
    threads:
        int(config['spl_n_cpus'])
    resources:
        job_id = 1,
        n_cpus = int(config['spl_n_cpus']),
        mem_gb = lambda wildcards, input, attempt: getMemoryRequest(attempt, 'spl'),
        time_h = lambda wildcards, input, attempt: getTimeRequest(attempt, 'spl')
    shell:
        '''
cd 3-align

cat > ./splitONT.sh <<EOF
#!/bin/sh
set -e

mkdir -p split

{PYTHON} {VERKKO}/scripts/process_reads.py \\\\
  partition split/ont {params.bases} {params.reads} {params.length} \\\\
  {input.ont}

touch ../{output.finished}
EOF

chmod +x ./splitONT.sh

./splitONT.sh > ../{log.err} 2>&1
        '''
