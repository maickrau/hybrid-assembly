#############################################################################
 #
 #  This file is part of Verkko, a software program that assembles
 #  whole-genome sequencing reads into telomere-to-telomere
 #  haplotype-resolved chromosomes.
 #
 #  Except as indicated otherwise, this is a 'United States Government
 #  Work', and is released in the public domain.
 #
 #  File 'README.licenses' in the root directory of this distribution
 #  contains full conditions and disclaimers.
 #
 ##

#
#  Rule combineONT combines the alignment results into a single file
#  then removes the individual alignment result files.
#
#  No computation is performed, only I/O.  No parameters, either.
#
#  Note that even though we're now officially done with alignments, the
#  splitONToutputs are used by consensus and cannot be removed here.
#

rule combineONT:
    input:
        split_finished = rules.splitONT.output.split_finished,
        alignments     = lambda wildcards: expand("3-align/aligned{nnnn}.gaf", nnnn = splitONToutputs(wildcards))
    output:
        alignments     = '3-align/alns-ont.gaf'
    log:
        err            = '3-align/combineONT.err'
    params:
        alignments     = lambda wildcards: expand("aligned{nnnn}.gaf", nnnn = splitONToutputs(wildcards)),
        keepinter      = config['keep_intermediate']
    threads:
        1
    resources:
        job_id = 1,
        n_cpus = 1,
        mem_gb = 4,
        time_h = 2
    shell:
        '''
cd 3-align

cat > ./combineONT.sh <<EOF
#!/bin/sh
set -e

cat {params.alignments} \\\\
> ../{output.alignments}

if [ {params.keepinter} = False ] ; then
  rm -f {params.alignments}

  rm -f ./aligned*.err
  rm -f ./aligned*.sh

  rm -f ./graph.index
fi

EOF

chmod +x ./combineONT.sh

./combineONT.sh > ../{log.err} 2>&1
        '''
