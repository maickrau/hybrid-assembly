#############################################################################
 #
 #  This file is part of Verkko, a software program that assembles
 #  whole-genome sequencing reads into telomere-to-telomere
 #  haplotype-resolved chromosomes.
 #
 #  Except as indicated otherwise, this is a 'United States Government
 #  Work', and is released in the public domain.
 #
 #  File 'README.licenses' in the root directory of this distribution
 #  contains full conditions and disclaimers.
 #
 ##

#
#  Rule buildPackages creates input packages for Canu's consensus module.
#

checkpoint buildPackages:
    input:
        hifi       = HIFI_READS,
        layout     = {rules.layoutContigs.output.mergedlayout},
        ont        = ONT_READS if config['withONT'] == "True" and config['consensus_bams'] == 'True' else {rules.emptyfile.output},
        ont_subset = {rules.extractONT.output.ont_subset} if config['withONT'] == "True" else {rules.emptyfile.output}
    output:
        tigmap     = "7-consensus/packages.tigName_to_ID.map",
        finished   = "7-consensus/packages.finished",
        packreport = "7-consensus/packages.report"
    log:
        err        = '7-consensus/buildPackages.err'
    params:
        consensus_bams = config['consensus_bams'] == 'True'
    threads:
        int(config['par_n_cpus'])
    resources:
        job_id = 1,
        n_cpus = int(config['par_n_cpus']),
        mem_gb = lambda wildcards, input, attempt: getBuildPackageMemoryRequest(attempt),
        time_h = lambda wildcards, input, attempt: getTimeRequest(attempt, 'par')
    shell:
        '''
cd 7-consensus

cat > ./buildPackages.sh <<EOF
#!/bin/sh
set -e

mkdir -p packages

if [ {params.consensus_bams} ]; then
    {VERKKO}/bin/layoutToPackage \\\\
        -layout ../{input.layout} \\\\
        -output packages/part###.cnspack \\\\
        -idmap  packages \\\\
        -partition 0.8 1.5 0.1 \\\\
        -reads {input.ont} {input.hifi} \\\\
    > ../{output.packreport} \\\\
    && \\\\
    touch ../{output.finished}
else
    {VERKKO}/bin/layoutToPackage \\\\
        -layout ../{input.layout} \\\\
        -output packages/part###.cnspack \\\\
        -idmap  packages \\\\
        -partition 0.8 1.5 0.1 \\\\
        -reads ../{input.ont_subset} {input.hifi} \\\\
    > ../{output.packreport} \\\\
    && \\\\
    touch ../{output.finished}
fi

EOF

chmod +x ./buildPackages.sh

./buildPackages.sh > ../{log.err} 2>&1
        '''

